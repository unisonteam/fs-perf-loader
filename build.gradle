apply plugin: 'java'

group = 'team.unison'
version = '1.0.4'
def formatGMT = new java.text.SimpleDateFormat("yyyy-MM-dd-HH:mm:ss")
formatGMT.setTimeZone(TimeZone.getTimeZone("GMT"))
def libraryVersion = version + "-" + formatGMT.format(new Date())

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}


task fatJar(type: Jar) {
  duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
  dependsOn('saveLibraryVersionAsResource')

  from sourceSets.main.output
  from "${projectDir}/scripts"

  manifest {
    attributes 'Main-Class': 'team.unison.perf.PerfLoaderMain',
            'Build-Time': new Date(),
            'Build-Version': libraryVersion
  }
  archiveClassifier = "fat"
  from {
    configurations.runtimeClasspath.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  with jar
}

artifacts {
  archives fatJar
}

task saveLibraryVersionAsResource {
  doLast {
    def resourcesDir = sourceSets.main.output.resourcesDir
    resourcesDir.mkdirs()
    new File(resourcesDir, "perf-loader.version").text = libraryVersion
  }
}

dependencies {
  compileOnly group: 'org.apache.hadoop', name: 'hadoop-common', version: '3.3.1'
  compileOnly group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: '3.3.1'
  compileOnly group: 'org.apache.hadoop', name: 'hadoop-hdfs-client', version: '3.3.1'

  implementation group: 'com.jcraft', name: 'jsch', version: '0.1.55'
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.5'
  implementation 'org.slf4j:slf4j-log4j12:2.0.5'
  implementation 'com.google.code.gson:gson:2.8.7'

  implementation platform('software.amazon.awssdk:bom:2.20.45')
  implementation 'software.amazon.awssdk:aws-core'
  implementation 'software.amazon.awssdk:s3'

  // https://github.com/aws/aws-sdk-java-v2/issues/1372
  implementation 'org.apache.httpcomponents:httpclient:4.5.14'
  implementation 'io.prometheus:simpleclient:0.16.0'
  implementation 'io.prometheus:simpleclient_pushgateway:0.16.0'

  testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
  testImplementation 'org.junit.platform:junit-platform-launcher:1.9.2'

  testImplementation group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'
  testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
}

test {
  useJUnitPlatform()

  testLogging.showStandardStreams = true
}